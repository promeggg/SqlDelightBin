import java.text.SimpleDateFormat

def siteUrl = 'https://github.com/OkBuilds/SqlDelightBin'
def gitUrl = 'https://github.com/OkBuilds/SqlDelightBin.git'
def publishGroup = 'com.github.okbuilds'
def publishVersion = '0.4.3'

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
    jcenter()
  }
  dependencies {
    classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
  }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: "com.github.johnrengelman.shadow"

mainClassName = 'com.github.piasy.sqldelight.bin.SqlDelightRunner'

shadowJar {
  classifier = null
}

sourceCompatibility = 1.8

repositories {
  mavenCentral()
}

dependencies {
  compile group: 'com.squareup.sqldelight', name: 'compiler', version: "${publishVersion}"
}

tasks.withType(JavaCompile) {
  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

group = publishGroup
version = publishVersion

Properties properties = new Properties()
// for wrapper project structure
File propertyFile = project.rootProject.file('bintray.properties')
if (propertyFile.exists()) {
  properties.load(propertyFile.newDataInputStream())
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.shadow

      artifact sourcesJar
      artifact javadocJar

      artifactId 'SqlDelightBin'
    }
  }
}

bintray {
  user = project.properties.get("bintray.user", properties.getProperty("bintray.user"))
  key = project.properties.get("bintray.apikey", properties.getProperty("bintray.apikey"))
  publications = ['mavenJava']
  dryRun = false
  publish = true
  pkg {
    repo = 'maven'
    name = 'SqlDelightBin'
    userOrg = 'okbuild'
    desc = 'A runnable jar that uses SqlDelight-Compiler to compile SQL into Java.'
    websiteUrl = siteUrl
    issueTrackerUrl = siteUrl + '/issues'
    vcsUrl = gitUrl
    licenses = ['MIT']
    labels = ['SqlDelight']
    publicDownloadNumbers = true
    version {
      name = publishVersion
      released = new SimpleDateFormat('yyyy-MM-dd\'T\'HH:mm:ss.SSSZZ').format(new Date())
      vcsTag = publishVersion
    }
  }
}

